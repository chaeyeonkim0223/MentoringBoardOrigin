plugins {
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.github.node-gradle.node" version "3.1.0"
	id 'java'
}
node {
	version = '16.13.0'
	npmVersion = '8.1.4'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	npmWorkDir = file("${project.buildDir}/npm")
}

//이미 빌드된 파일 삭제
task deleteVueArtifact(type: Delete) {
	delete "src/main/resources/static"
}

//npm install
task frontNpmInstall(type: NpmTask) {
	workingDir = file("${project.projectDir}/src/frontend")
	args = ['install']
}

//npm run build
task npmBuild(type: NpmTask, dependsOn: ['deleteVueArtifact', 'frontNpmInstall']) {
	workingDir = file("${project.projectDir}/src/frontend")
	args = ['run', 'build']
}

processResources.dependsOn 'npmBuild'

group = 'com.ktds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

//lombok 설정 추가 시작
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}
//lombok 설정 추가 끝

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
	implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
	//validation 어노테이션 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	//lombok 라이브러리 추가 시작
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	//lombok 라이브러리 추가 끝
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	developmentOnly("org.springframework.boot:spring-boot-devtools")
	//mybatis 라이브러리 추가
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.4'
	//postgresql 라이브러리 추가
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'


}

test {
	useJUnitPlatform()
}
